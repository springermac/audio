# Maintainer: Saul Ibarra Corretge <saghul@gmail.com>

_realname=pyqtchart
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python3-${_realname}"
pkgver=5.11.3
pkgrel=0
pkgdesc="Qt5 bindings for Python (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.riverbankcomputing.co.uk/software/pyqtchart"
makedepends=("${MINGW_PACKAGE_PREFIX}-python2-sip"
             "${MINGW_PACKAGE_PREFIX}-python3-sip"
             "${MINGW_PACKAGE_PREFIX}-python2-pyqt5"
             "${MINGW_PACKAGE_PREFIX}-python3-pyqt5"
             "${MINGW_PACKAGE_PREFIX}-make")
options=('strip' 'staticlibs')
source=(https://www.riverbankcomputing.com/static/Downloads/PyQtChart/${pkgver}/PyQtChart_gpl-${pkgver}.tar.gz
        001-mingw-python.patch)
sha256sums=('89abf5e48c79c4db42a01f80433bb97d64b117f801be5e4cb0b9839efc99472f'
            '2125850c6e1f73a0d7325425d083466e99cf55a369a55092748f4f97c63668ed')

prepare() {
 cd "${srcdir}"/PyQtChart_gpl-${pkgver}

  #Apply patches
  patch -p1 -i "${srcdir}"/001-mingw-python.patch
}

build() {
  for builddir in python3-${MINGW_CHOST}; do
    [[ -d $builddir ]] && rm -rf ${builddir}
    cp -r PyQtChart_gpl-${pkgver} ${builddir}
    pushd ${builddir} > /dev/null
    ${MINGW_PREFIX}/bin/${builddir%-${MINGW_CHOST}} configure.py \
      --pyqt-sipdir ${MINGW_PREFIX}/share/sip/PyQt5 \
      --verbose
    make V=1
    popd > /dev/null
  done
}

package_python3-pyqtchart(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=("${MINGW_PACKAGE_PREFIX}-python3-sip" "${MINGW_PACKAGE_PREFIX}-pyqt5" "${MINGW_PACKAGE_PREFIX}-python3")

  cd python3-${MINGW_CHOST}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

# Wrappers
package_mingw-w64-i686-python3-pyqtchart() {
  package_python3-pyqtchart
}

package_mingw-w64-x86_64-python3-pyqtchart() {
  package_python3-pyqtchart
}