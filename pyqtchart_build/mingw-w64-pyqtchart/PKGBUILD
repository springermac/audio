# Maintainer: Saul Ibarra Corretge <saghul@gmail.com>

_realname=pyqtchart
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-python3-${_realname}"
pkgver=5.13.1
pkgrel=0
pkgdesc="Qt5 bindings for Python (mingw-w64)"
arch=('any')
license=('GPL')
url="http://www.riverbankcomputing.co.uk/software/pyqtchart"
makedepends=(#"${MINGW_PACKAGE_PREFIX}-python2-sip"
             "${MINGW_PACKAGE_PREFIX}-python3-sip"
             #"${MINGW_PACKAGE_PREFIX}-python2-pyqt5"
             "${MINGW_PACKAGE_PREFIX}-python3-pyqt5"
             "${MINGW_PACKAGE_PREFIX}-make")
options=('strip' 'staticlibs')
source=(https://www.riverbankcomputing.com/static/Downloads/PyQtChart/${pkgver}/PyQtChart-${pkgver}.tar.gz
        001-mingw-python.patch)
sha256sums=('49960a1483527857b38c1527f9b6328d30bdcc84521f579c0a561a892f54130e'
            'e8bb8826d157cdd9560d11442f885b28d5b784ad13a223c1cd80d8001a47c93c')

prepare() {
 cd "${srcdir}"/PyQtChart-${pkgver}

  #Apply patches
  patch -p1 -i "${srcdir}"/001-mingw-python.patch
}

build() {
  for builddir in python3-${MINGW_CHOST}; do
    [[ -d $builddir ]] && rm -rf ${builddir}
    cp -r PyQtChart-${pkgver} ${builddir}
    pushd ${builddir} > /dev/null
    ${MINGW_PREFIX}/bin/${builddir%-${MINGW_CHOST}} configure.py \
      --no-qsci-api \
      --verbose
    make V=1
    popd > /dev/null
  done
}

package_python3-pyqtchart(){
  pkgdesc="A set of Python 3.x bindings for the Qt5 toolkit"
  depends=("${MINGW_PACKAGE_PREFIX}-python3-sip" "${MINGW_PACKAGE_PREFIX}-python3-pyqt5" "${MINGW_PACKAGE_PREFIX}-python3")

  cd python3-${MINGW_CHOST}
  # INSTALL_ROOT is needed for the QtDesigner module, the other Makefiles use DESTDIR
  make -j1 DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

# Wrappers
package_mingw-w64-i686-python3-pyqtchart() {
  package_python3-pyqtchart
}

package_mingw-w64-x86_64-python3-pyqtchart() {
  package_python3-pyqtchart
}